BASE <http://world-fr.openfoodfacts.org/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/> 
PREFIX fun: <http://w3id.org/sparql-generate/fn/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/> 
PREFIX food: <http://world-fr.openfoodfacts.org/> 
GENERATE { 
?allergen a food:Allergen ;
                    owl:sameAs ?wikidata_url; 
.

GENERATE { ?allergen 
rdfs:label ?name_lang . }
ITERATOR iter:JSONListKeys(?names) AS ?lang
WHERE {
BIND (fun:JSONPath(?names, CONCAT("$.",?lang)) AS ?name) 
BIND (STRLANG(?name,?lang) AS ?name_lang)
} .

}

SOURCE <https://static.openfoodfacts.org/data/taxonomies/allergens.json> AS ?source 
ITERATOR iter:JSONListKeys(?source) AS ?en_allergen_key 
WHERE { 
BIND (IRI(CONCAT("http://world-fr.openfoodfacts.org/allergen/",?en_allergen_key)) as ?allergen) 
BIND (fun:JSONPath(?source,CONCAT("$.",?en_allergen_key)) AS ?en_allergen) 

BIND (fun:JSONPath(?en_allergen,"$.name") AS ?names) 
BIND (IRI(CONCAT("http://www.wikidata.org/entity/",fun:JSONPath(?en_allergen,"$.wikidata.en"))) as ?wikidata_url) 
}
